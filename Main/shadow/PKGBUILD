# -*- mode: sh -*-
pkgname=shadow
pkgver=4.6
pkgrel=1
pkgdesc="Password and account management tool suite with support for shadow files and PAM"
arch=('x86_64')
url='https://github.com/shadow-maint/shadow'
license=('BSD')
_deps=('acl' 'bash' 'linux-pam' 'cracklib')
depends=()
_makedeps=('libxslt' 'docbook-xsl' 'gnome-doc-utils')
makedepends=()
backup=(etc/login.defs
        etc/pam.d/{chage,passwd,shadow,useradd,usermod,userdel}
        etc/pam.d/{groupadd,groupdel,groupmod}
        etc/pam.d/groupmems
        etc/default/useradd)
options=(strip)
install='shadow.install'
source=(https://github.com/shadow-maint/shadow/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz
        defaults.pam
        login
        su
        passwd
       )
groups=('lfs-base')

for _d in "${_deps[@]}";do
  if [[ -n "$(pacman -Ss "^${_d}$" 2>/dev/null)" ]];then
    depends+=( "${_d}")
  else
    optdepends+=( "${_d}")
  fi
done
for _m in "${_makedeps[@]}";do
  if [[ -n "$(pacman -Ss "^${_m}$" 2>/dev/null)" ]];then
    makedepends+=( "${_m}")
  else
    optdepends+=( "${_m}")
  fi
done

prepare() {
  cd "$pkgname"-"$pkgver"

  # need to offer these upstream
  #patch -Np1 <"$srcdir/xstrdup.patch"
  #patch -Np1 <"$srcdir/shadow-strncpy-usage.patch"

  # supress etc/pam.d/*, we provide our own
  sed -i '/^SUBDIRS/s/pam\.d//' etc/Makefile.in

  # Disable the installation of the groups program and its man pages, as Coreutils provides a better version.
  # Also Prevent the installation of manual pages that were already installed by the man pages package
  sed -i 's/groups$(EXEEXT) //' src/Makefile.in
  find man -name Makefile.in -exec sed -i 's/groups\.1 / /' {} \;
  find man -name Makefile.in -exec sed -i 's/getspnam\.3 / /' {} \;
  find man -name Makefile.in -exec sed -i 's/passwd\.5 / /' {} \;

  # use the more secure SHA-512 method of password encryption, which also allows passwords longer than 8 characters.
  # It is also necessary to change the obsolete /var/spool/mail location for user mailboxes that
  # Shadow uses by default to the /var/mail location used currently
  sed -i -e 's@#ENCRYPT_METHOD DES@ENCRYPT_METHOD SHA512@' \
      -e 's@/var/spool/mail@/var/mail@' etc/login.defs

  if [[ -z "$(pacman -Qq cracklib 2>&1 >/dev/null)" ]];then
    sed -i 's@DICTPATH.*@DICTPATH\t/lib/cracklib/pw_dict@' etc/login.defs
  fi

  # a minor change to make the first group number generated by useradd 1000
  sed -i 's/1000/999/' etc/useradd
  
  _confargs=()

  if [[ -z "$(pacman -Qq linux-pam 2>&1 >/dev/null)" ]];then
    _confargs+=( '--with-libpam')
  else
    _confargs+=( '--without-libpam')
  fi
  
  if [[ -z "$(pacman -Qq gnome-doc-utils 2>&1 >/dev/null)" ]];then
    _confargs+=( '--enable-man')
  else
    _confargs+=( '--disable-man')
  fi

  ./configure \
    LIBS="-lcrypt" \
    --sysconfdir=/etc \
    --with-group-name-max-length=32 \
    ${_confargs[@]}
}

build() {
  cd "$pkgname"-"$pkgver"
  make
}

package() {
  cd "$pkgname"-"$pkgver"

  make DESTDIR="$pkgdir" install

  install -v -dm755 "$pkgdir"/bin
  mv -v "$pkgdir"/usr/bin/passwd "$pkgdir"/bin

  if [[ -z "$(pacman -Qq linux-pam 2>&1 >/dev/null)" ]];then
    # PAM config - custom
    install -dm755 "$pkgdir/etc/pam.d"
    install -t "$pkgdir/etc/pam.d" -m644 "$srcdir"/{passwd,login,su}

    # PAM config - from tarball
    install -Dm644 etc/pam.d/groupmems "$pkgdir/etc/pam.d/groupmems"

    # we use the 'useradd' PAM file for other similar utilities
    for file in chage chfn groupadd groupdel groupmod shadow \
                      useradd usermod userdel; do
      install -Dm644 "$srcdir/defaults.pam" "$pkgdir/etc/pam.d/$file"
    done
  fi
}

md5sums=('b491fecbf1232632c32ff8f1437fd60e'
         '41f81e9307110ff0e2bf32f50ecb1fe0'
         '53c9b49c843338fdef1f6d2364cb6a3a'
         '09a07847015540190e8faf6aedb417e5'
         'a205549bec2e6990af562d02b3fc3298')
