# -*- mode:sh -*-
pkgname=bzip2
pkgver=1.0.6
pkgrel=1
pkgdesc="A high-quality data compression program"
arch=('x86_64')
license=('custom')
url="http://sources.redhat.com/bzip2"
depends=('glibc')
source=(http://anduin.linuxfromscratch.org/LFS/${pkgname}-${pkgver}.tar.gz
        http://www.linuxfromscratch.org/patches/lfs/development/${pkgname}-${pkgver}-install_docs-1.patch)
groups=('lfs-base')

if [[ -n "$(pacman -Ss ^sh$ 2>/dev/null)" ]];then
  makedepends+=( 'sh')
else
  optdepends+=( 'sh')
fi

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  # patch that will install the documentation for this package
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-install_docs-1.patch

  # ensures installation of symbolic links are relative
  sed -i 's@\(ln -s -f \)$(PREFIX)/bin/@\1@' Makefile

  # Ensure the man pages are installed into the correct location
  sed -i "s@(PREFIX)/man@(PREFIX)/share/man@g" Makefile
}

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -f Makefile-libbz2_so
  make clean
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -v -dm755 $pkgdir/{bin,lib}
  install -v -dm755 $pkgdir/usr/{bin,include,share/man/man1}

  install -v -m755 bzip2-shared $pkgdir/bin/bzip2
  install -v -m755 bzip2recover bzdiff bzgrep bzmore $pkgdir/usr/bin
  ln -svf bzip2 $pkgdir/bin/bunzip2
  ln -svf bzip2 $pkgdir/bin/bzcat

  install -v -m755 libbz2.so.1.0.6 $pkgdir/lib
  ln -sv libbz2.so.1.0.6 $pkgdir/lib/libbz2.so
  ln -sv libbz2.so.1.0.6 $pkgdir/lib/libbz2.so.1
  ln -sv libbz2.so.1.0.6 $pkgdir/lib/libbz2.so.1.0

  install -v -m644 bzlib.h $pkgdir/usr/include/

  install -v -m644 bzip2.1 $pkgdir/usr/share/man/man1/
  ln -svf bzip2.1 $pkgdir/usr/share/man/man1/bunzip2.1
  ln -svf bzip2.1 $pkgdir/usr/share/man/man1/bzcat.1
  ln -svf bzip2.1 $pkgdir/usr/share/man/man1/bzip2recover.1
}

md5sums=('00b516f4704d4a7cb50a1d97e6e8e15b'
         '6a5ac7e89b791aae556de0f745916f7f')
