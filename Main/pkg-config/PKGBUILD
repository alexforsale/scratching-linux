# -*- mode: sh -*-
pkgname=pkg-config
pkgver=0.29.2
pkgrel=1
pkgdesc="Tool for passing the include path and/or library paths to build tools during the configure and make file execution"
url="https://www.freedesktop.org/wiki/Software/pkg-config"
license=(GPL)
arch=(x86_64)
_deps=('sh')
depends=('glibc')
_makedeps=('glib2')
makedepends=()
provides=(pkg-config pkgconfig)
conflicts=(pkgconf)
replaces=(pkgconf)
source=(https://pkg-config.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz)
groups=('lfs-base')

for _m in "${_makedeps[@]}";do
    if [[ -n "$(pacman -Ss "^${_m}$" 2>/dev/null)" ]];then
        makedepends+=( "${_m}")
    else
        optdepends+=( "${_m}")
    fi
done

for _d in "${_deps[@]}";do
    if [[ -n "$(pacman -Ss "^${_d}$" 2>/dev/null)" ]];then
        makedepends+=( "${_d}")
    else
        optdepends+=( "${_d}")
    fi
done

_confargs=("--prefix=/usr" "--disable-host-tool" "--docdir=/usr/share/doc/${pkgname}-${pkgver}")

# set default search path
_confargs+=( '--with-pc-path=/usr/lib/pkgconfig:/usr/share/pkgconfig')
if [[ ! $(pacman -Qq glib2 2>/dev/null) ]];then
    _confargs+=( "--with-internal-glib")
fi

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure ${_confargs[@]}
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make
}

check(){
    cd ${srcdir}/${pkgname}-${pkgver}
    make check    
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
}

md5sums=('f6e931e319531b736fadc017f470e68a')
