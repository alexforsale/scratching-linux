# -*- mode: sh -*-
pkgname=binutils
pkgver=2.31.1
pkgrel=1
pkgdesc='contains a linker, an assembler, and other tools for handling object files'
arch=(x86_64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=('glibc'
         'zlib'
        )
checkdepends=('bc')
conflicts=(binutils-multilib)
replaces=(binutils-multilib)
options=(staticlibs !distcc !ccache)
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz)
groups=('lfs-base')

if [[ -n "$(pacman -Ss ^dejagnu$ 2>/dev/null)" ]];then
  checkdepends+=( 'dejagnu')
else
  optdepends+=( 'dejagnu: checkdepend')
fi

prepare(){
  mkdir -p binutils-build

  cd binutils-${pkgver}
  cd ${srcdir}/binutils-build
  "$srcdir/binutils-${pkgver}/configure" \
    --prefix=/usr \
    --enable-gold \
    --enable-ld=default \
    --enable-plugins \
    --enable-shared \
    --disable-werror \
    --enable-64-bit-bfd \
    --with-system-zlib
}

build() {
  cd binutils-build
  make tooldir=/usr
}

check() {
  cd binutils-build
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # ignore failures in gold testsuite...
  if [[ -n "$(pacman -Qq dejagnu 2>/dev/null)" ]];then
    make -k LDFLAGS="" check || true
  fi
}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install
}

md5sums=('5b7c9d4ce96f507d95c1b9a255e52418')
