#-*- mode: sh -*-
pkgname=libcap
pkgver=2.25
pkgrel=1
pkgdesc='POSIX 1003.1e capabilities'
arch=('x86_64')
url='http://sites.google.com/site/fullycapable/'
license=('GPL2')
depends=('glibc' 'attr')
makedepends=('linux-api-headers')
source=(https://kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-$pkgver.tar.xz)
groups=('lfs-base')

prepare() {
  cd $pkgname-$pkgver

  sed -i "/SBINDIR/s#sbin#bin#" Make.Rules
  
  # use our buildflags
  sed -i "s/CFLAGS :=/CFLAGS += \$(CPPFLAGS) /" Make.Rules
  sed -i "s/LDFLAGS :=/LDFLAGS +=/" Make.Rules

  # Prevent a static library from being installed
  sed -i '/install.*STALIBNAME/d' libcap/Makefile
}

build() {
  make -C $pkgname-$pkgver RAISE_SETFCAP=no lib=lib BUILD_GPERF=no
}

package() {
  cd $pkgname-$pkgver
  make prefix=/usr lib=/lib DESTDIR="$pkgdir" RAISE_SETFCAP=no \
       BUILD_GPERF=no install

  install -v -dm755 "$pkgdir"/lib
  mv -v "$pkgdir"/usr/lib/libcap.so.* "$pkgdir"/lib
  ln -sfv ../../lib/$(readlink "$pkgdir"/usr/lib/libcap.so) "$pkgdir"/usr/lib/libcap.so

  install -Dm644 pam_cap/capability.conf \
          "$pkgdir"/usr/share/doc/$pkgname/capability.conf.example
}

md5sums=('6666b839e5d46c2ad33fc8aa2ceb5f77')
