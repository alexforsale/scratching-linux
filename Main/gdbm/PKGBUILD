#-*- mode: sh -*-
pkgname=gdbm
pkgver=1.17
pkgrel=1
pkgdesc='GNU database library'
url='http://www.gnu.org/software/gdbm/gdbm.html'
license=(GPL3)
arch=(x86_64)
depends=(glibc sh)
options=(!makeflags)
source=(http://ftp.gnu.org/gnu/gdbm/${pkgname}-${pkgver}.tar.gz)
groups=('lfs-base')

prepare(){
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-libgdbm-compat
}

build() {
  cd $pkgname-$pkgver
  make
}

check() {
  cd $pkgname-$pkgver
  make check || true
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # create symlinks for compatibility
  install -dm755 "$pkgdir/usr/include/gdbm"
  ln -sf ../gdbm.h "$pkgdir/usr/include/gdbm/gdbm.h"
  ln -sf ../ndbm.h "$pkgdir/usr/include/gdbm/ndbm.h"
  ln -sf ../dbm.h  "$pkgdir/usr/include/gdbm/dbm.h"
}

md5sums=('f20ce117abc6f302ecf62c34d41c1ecf')
