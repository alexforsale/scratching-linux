#-*- mode: sh -*-
pkgname=openssl
_ver=1.1.0i
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('perl')
optdepends=('make-ca')
backup=('etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz"        
        ca-dir.patch)
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491')
groups=('lfs-base')

prepare() {
  cd "$srcdir/$pkgname-$_ver"

  # set ca dir to /etc/ssl by default
  patch -p0 -i "$srcdir/ca-dir.patch"

  if [ "${CARCH}" == 'x86_64' ]; then
    openssltarget='linux-x86_64'
    optflags='enable-ec_nistp_64_gcc_128'
  elif [ "${CARCH}" == 'i686' ]; then
    openssltarget='linux-elf'
    optflags=''
  fi

  # mark stack as non-executable: http://bugs.archlinux.org/task/12434
  ./Configure --prefix=/usr --openssldir=/etc/ssl --libdir=lib \
	      shared zlib-dynamic ${optflags} \
	      "${openssltarget}" \
	      "-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"
}

build() {
  cd "$srcdir/$pkgname-$_ver"
  make depend
  make
}

check() {
  cd "$srcdir/$pkgname-$_ver"
  # the test fails due to missing write permissions in /etc/ssl
  # revert this patch for make test
  patch -p0 -R -i "$srcdir/ca-dir.patch"
  make test
  patch -p0 -i "$srcdir/ca-dir.patch"
}

package() {
  cd "$srcdir/$pkgname-$_ver"
  sed -i '/INSTALL_LIBS/s/libcrypto.a libssl.a//' Makefile
  make DESTDIR=$pkgdir MANDIR=/usr/share/man MANSUFFIX=ssl install
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  mv -v $pkgdir/usr/share/doc/openssl $pkgdir/usr/share/doc/${pkgname}-${_ver}
  cp -vfr doc/* $pkgdir/usr/share/doc/${pkgname}-${_ver}
}

md5sums=('9495126aafd2659d357ea66a969c3fe1'
         '182ad163458781ed9bd4b342c5724ece')
