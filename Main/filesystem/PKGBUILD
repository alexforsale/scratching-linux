# -*- mode: sh -*-
pkgname=filesystem
pkgver=8.3
pkgrel=1
pkgdesc='base filesystem'
arch=('any')
license=('GPL')
url='http://www.linuxfromscratch.org/lfs'
source=('passwd' 'group' 'profile' 'shells' 'os-release' 'fstab' 'securetty'
        'dotprofile' 'dircolors.sh' 'extrapaths.sh' 'umask.sh' 'i18n.sh' 'opt.conf' 'shadow')
backup=('etc/fstab' 'etc/group' 'etc/gshadow' 'etc/passwd' 'etc/profile' 'etc/securetty'
        'etc/shadow' 'etc/shells')
groups=('lfs-base')

package() {
    cd "$pkgdir"

    # root filesystem

    for d in bin boot dev etc home mnt lib/firmware usr var opt srv/http run;do
        install -v -d -m755 ${d}
    done
    install -v -d -m 555 proc
    install -v -d -m 555 sys
    install -v -d -m 0750 root
    install -v -d -m 1777 tmp

    install -d -m555 -g 45 srv/ftp
    ln -s ../proc/self/mounts etc/mtab

    install -v -d -m 755 usr/{,local/}{bin,include,lib,sbin,src}
    install -v -d -m 755 usr/{,local/}share/{color,dict,doc,info,locale,man}
    install -v -d -m 755 usr/{,local/}share/{misc,terminfo,zoneinfo}
    install -v -d -m 755 usr/libexec
    install -v -d -m 755 usr/{,local/}share/man/man{1..8}

    install -v -dm755 usr/lib32
    install -v -dm755 usr/local/lib32

    [[ $CARCH = 'x86_64' ]] && {
        ln -s lib usr/lib64
        ln -s lib usr/local/lib64
        ln -s usr/lib32 lib32
    }

    install -v -dm755 usr/lib32

    install -v -d -m1777 var/{tmp,spool/mail}
    install -v -d -m 755 var/log

    ln -s spool/mail var/mail
    ln -s ../run var/run
    ln -s ../run/lock var/lock
    
    install -v -d -m 755 var/{opt,cache,lib/{color,misc,locate},local}

    install -v -d etc/{ld.so.conf.d,skel,profile.d}

    install -m644 "${srcdir}"/opt.conf etc/ld.so.conf.d/
    for f in passwd group profile shells fstab shadow securetty;do
        install -m644 "${srcdir}"/${f} etc/
    done

    install -v -d -m 755 opt/{bin,doc,include,info,lib,man}
    
    install -Dm644 "$srcdir"/os-release usr/lib/os-release

    install -m644 "${srcdir}"/dotprofile etc/skel/.profile

    for f in dircolors.sh extrapaths.sh umask.sh i18n.sh;do
        install -m644 "${srcdir}"/${f} etc/profile.d/${f}
    done

    for f in dircolors.sh extrapaths.sh umask.sh i18n.sh;do
        install -m644 "${srcdir}"/${f} etc/profile.d/${f}
    done
}

md5sums=('46393e48778149b3a285f9f379987f16'
         '01bf936475ca307378514036244d3d71'
         '2a9c0f61b70f19b17cb4a5af9b9a23a4'
         '2ed0cf9d92e8a8fa49a41918b8a0207f'
         'fe068520f0a6ddc18749d60f0c953857'
         'e88c301afb37a590c8626c4d48287699'
         'f04bcb2803afc4dcb95670fe87343b4d'
         '325ff68a73f2bbda1fbf8d5d735030b2'
         'c374459fd914c975b41e68902a40c9e1'
         'ea3eb0aa7b11cd459bdc725092c1331f'
         'f1dd87922e8fddfe73ef20ae76c2f777'
         '9ac887b370aa53cbcd0a7b8e0ec7a828'
         '4e630f1d63bc4ee2178467e71ddd3ebf'
         '2ef2429c2c98d0883906aa6f5363be80')
