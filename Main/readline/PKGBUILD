# -*- mode: sh -*-
pkgname=readline
pkgver=7.0
pkgrel=1
pkgdesc='a set of libraries that offers command-line editing and history capabilities'
arch=('x86_64')
url='http://tiswww.case.edu/php/chet/readline/rltop.html'
license=('GPL')
backup=('etc/inputrc')
depends=('glibc')
provides=('libhistory.so'
          'libreadline.so')
options=('!emptydirs')
source=(https://ftp.gnu.org/gnu/readline/readline-$pkgver.tar.gz
        inputrc
        readline.sh)
groups=('lfs-base')

if [[ -n "$(pacman -Ss ncurses$ 2>/dev/null)" ]];then
  depends+=( 'ncurses' 'libncursesw.so')
else
  optdepends+=( 'ncurses')
fi

prepare() {
    cd ${pkgname}-${pkgver}

    # Reinstalling Readline will cause the old libraries to be moved to <libraryname>.old.
    # While this is normally not a problem, in some cases it can trigger a linking bug in ldconfig.
    sed -i '/MV.*old/d' Makefile.in
    sed -i '/{OLDSUFF}/c:' support/shlib-install

    ./configure --prefix=/usr \
                --disable-static \
                --docdir=/usr/share/doc/${pkgname}-${pkgver}
}

if [[ -z "$(pacman -Qq ncurses 2>&1 >/dev/null)" ]];then
    _libs="-lncurses"
elif [[ -f /tools/bin/ncursesw6-config ]];then
    _libs="-L/tools/lib -lncursesw"
fi

build(){
    cd ${pkgname}-${pkgver}
    make SHLIB_LIBS=${_libs}
}

package() {
    make -C ${pkgname}-${pkgver} SHLIB_LIBS=${_libs} DESTDIR="${pkgdir}" install
    install -Dm644 inputrc "${pkgdir}"/etc/inputrc
    install -Dm644 readline.sh "${pkgdir}"/etc/profile.d/readline.sh

    install -v -d -m755 ${pkgdir}/lib
    mv -v ${pkgdir}/usr/lib/lib{readline,history}.so.* ${pkgdir}/lib
    ln -sfv ../../lib/$(readlink ${pkgdir}/usr/lib/libreadline.so) ${pkgdir}/usr/lib/libreadline.so
    ln -sfv ../../lib/$(readlink ${pkgdir}/usr/lib/libhistory.so ) ${pkgdir}/usr/lib/libhistory.so
}

md5sums=('205b03a87fc83dab653b628c59b9fc91'
         '3cea0a1d246504dacd93402ccb99cd76'
         'b2ecbed00bbdf66d4a85383396805dbd')
