#-*-mode:sh-*-
pkgbase=python-setuptools
pkgname=('python-setuptools' 'python2-setuptools')
pkgver=40.6.2
pkgrel=1
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages"
arch=('any')
license=('PSF')
url="https://pypi.org/project/setuptools/"
_deps=('appdirs' 'packaging' 'pip' 'pyparsing' 'six')
makedepends=('python-six' 'python2-six' 'python2-pyparsing' 'python-pyparsing')
_checkdeps=('mock' 'pip' 'pytest-fixture-config' 'pytest-flake8'
            'pytest-runner' 'pytest-virtualenv' 'wheel')
checkdepends=()
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pypa/setuptools/archive/v$pkgver.tar.gz")
sha512sums=('4b88a9fa9fc070c48d416ce432b4a3316e78de4cdc3bf8476cf234f0733ab14b79e4433b7775db8ca22a25cf1b025690a7dbeaf973da88576f09711803ee7c72')

if [[ -n "$(pacman -Ss ^git$)" ]];then
    makedepends+=( 'git')
    checkdepends+=( 'git')
fi
for _d in ${_deps[@]};do
    if [[ -n "$(pacman -Ss ${_d[@]/#/python-})" ]];then
        makedepends+=( "${_d[@]/#/python-}")
        depends+=( "${_d[@]/#/python-}")
    fi
    if [[ -n "$(pacman -Ss ${_d[@]/#/python2-})" ]];then
        makedepends+=( "${_d[@]/#/python2-}")
        depends+=( "${_d[@]/#/python2-}")
    fi
done
for _c in ${_checkdeps[@]};do
    if [[ -n "$(pacman -Ss ${_c[@]/#/python-})" ]];then
        checkdepends+=( "${_c[@]/#/python-}")
    fi
    if [[ -n "$(pacman -Ss ${_c[@]/#/python2-})" ]];then
        checkdepends+=( "${_c[@]/#/python-}")
    fi
done

export SETUPTOOLS_INSTALL_WINDOWS_SPECIFIC_FILES=0

prepare() {
  rm -r setuptools-$pkgver/{pkg_resources,setuptools}/{extern,_vendor}

  # Upstream devendoring logic is badly broken, see:
  # https://bugs.archlinux.org/task/58670
  # https://github.com/pypa/pip/issues/5429
  # https://github.com/pypa/setuptools/issues/1383
  # The simplest fix is to simply rewrite import paths to use the canonical
  # location in the first place
  for _module in setuptools pkg_resources '' ; do
    find setuptools-$pkgver/$_module -name \*.py -exec sed -i \
         -e 's/from '$_module.extern' import/import/' \
         -e 's/from '$_module.extern'./from /' \
         -e 's/import '$_module.extern'./import /' \
         -e "s/__import__('$_module.extern./__import__('/" \
         {} +
  done

  # Remove post-release tag since we are using stable tags
  sed -e '/tag_build = .post/d' \
      -e '/tag_date = 1/d' \
      -i setuptools-$pkgver/setup.cfg

  # 'Clean' installation is expected to fail since we removed bundled packages
  sed -i '/^def test_clean_env_install/i import pytest\n\n@pytest.mark.xfail' setuptools-$pkgver/setuptools/tests/test_virtualenv.py

  # Tests failed. Importing an unbundled new setuptools in a virtualenv does not work, but this won't
  # affect normal virtualenv usage (which don't have to import the unbundled setuptools in *current*
  # dir.
  sed -e '/^def test_pip_upgrade_from_source/i @pytest.mark.xfail' \
      -e '/^def test_test_command_install_requirements/i @pytest.mark.xfail' \
      -e '/^def test_no_missing_dependencies/i @pytest.mark.xfail' \
      -i setuptools-$pkgver/setuptools/tests/test_virtualenv.py

  cp -a setuptools-$pkgver{,-py2}

  cd "$srcdir"/setuptools-$pkgver
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python3|" setuptools/command/easy_install.py

  cd "$srcdir"/setuptools-$pkgver-py2
  sed -i -e "s|^#\!.*/usr/bin/env python|#!/usr/bin/env python2|" setuptools/command/easy_install.py
}

build() {
  cd "$srcdir"/setuptools-$pkgver
  python bootstrap.py
  python setup.py build

  cd "$srcdir"/setuptools-$pkgver-py2
  python2 bootstrap.py
  python2 setup.py build
}

check() { (
          # Workaround UTF-8 tests by setting LC_CTYPE
          export LC_CTYPE=en_US.UTF-8

          # https://github.com/pypa/setuptools/pull/810
          export PYTHONDONTWRITEBYTECODE=1

          cd "$srcdir"/setuptools-$pkgver
          python setup.py pytest

          cd "$srcdir"/setuptools-$pkgver-py2
          python2 setup.py pytest
        )}

package_python-setuptools() {
  provides=('python-distribute')
  replaces=('python-distribute')

  cd "$srcdir"/setuptools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-setuptools() {
  provides=('python2-distribute')
  replaces=('python2-distribute')

  cd "$srcdir"/setuptools-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  rm "$pkgdir"/usr/bin/easy_install
}
