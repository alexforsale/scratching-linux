#-*-mode:sh-*-
pkgname=('python-pip' 'python2-pip')
pkgver=18.1
pkgrel=1
pkgdesc="The PyPA recommended tool for installing Python packages"
url="https://pip.pypa.io/"
arch=('any')
license=('MIT')
_deps=('appdirs' 'cachecontrol' 'colorama' 'distlib' 'distro' 'html5lib' 'lockfile'
       'msgpack' 'six' 'packaging' 'pep517' 'progress' 'pytoml' 'retrying' 'requests' 'urllib3'
       'webencodings')
makedepends=('python-setuptools' 'python2-setuptools')
_checkdeps=('pytest-runner' 'scripttest' 'virtualenv' 'pretend' 'yaml' 'mock' 'freezegun')
checkdepends=()
source=("pip-${pkgver}.tar.gz::https://github.com/pypa/pip/archive/${pkgver}.tar.gz")
sha512sums=('ddb67ad52c2b4f0cae89e11bd90dbd20275fce0ee34a8e0929210a8907cd61c2298221a4ddf1e7b196b0274241eb88097decbbb84196ab30dd4e979e7378d91f')

if [[ -n "$(pacman -Ss ^python2-ipaddress$ 2>/dev/null)" ]];then
  makedepends+=( 'python2-ipaddress')
fi
if [[ -n "$(pacman -Ss ^python-sphinx$ 2>/dev/null)" ]];then
  makedepends+=( 'python-sphinx')
fi

for _d in ${_deps[@]};do
  if [[ -n "$(pacman -Ss "^${_d[@]/#/python-}$" 2>/dev/null)" ]];then
    makedepends+=( "${_d[@]/#/python-}")
    depends+=( "${_d[@]/#/python-}")
  fi
  if [[ -n "$(pacman -Ss "^${_d[@]/#/python2-}$" 2>/dev/null)" ]];then
    makedepends+=( "${_d[@]/#/python2-}")
    depends+=( "${_d[@]/#/python2-}")
  fi
done
for _c in ${_checkdeps[@]};do
  if [[ -n "$(pacman -Ss "^${_c[@]/#/python-}$" 2>/dev/null)" ]];then
    checkdepends+=( "${_c[@]/#/python-}")
  fi
done

shopt -s extglob
prepare() {
  cd "$srcdir/pip-$pkgver"

  rm -rf src/pip/_vendor/!(__init__.py)
  sed -i -e 's/DEBUNDLED = False/DEBUNDLED = True/' \
      -e '/cachecontrol/a\    vendored("pep517")' \
      src/pip/_vendor/__init__.py
}

build() {
  cd "$srcdir/pip-$pkgver"

  python setup.py build
  python2 setup.py build

  if [[ -n "$(pacman -Qq python-sphinx 2>/dev/null)" ]];then
    cd docs/
    PYTHONPATH="$srcdir/pip-$pkgver/src/" sphinx-build -W -b man -d build/doctrees/man man build/man -c html
    mkdir -p build/man-pip2
    cd build/man
    for manfile in *; do
      sed 's/pip/pip2/g;s/PIP/PIP2/g' $manfile > ../man-pip2/${manfile/pip/pip2}
    done
  fi
}

check() {
  cd "$srcdir/pip-$pkgver"
  python setup.py pytest || warning "Tests failed"
}

package_python-pip() {
  cd "$srcdir/pip-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir"

  install -D -m644 LICENSE.txt \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  if [[ -n "$(pacman -Qq python-sphinx 2>/dev/null)" ]];then
    install -Dm644 -t "$pkgdir"/usr/share/man/man1 docs/build/man/*
  fi
  
  PYTHONPATH="$pkgdir"/usr/lib/python3.7/site-packages "$pkgdir"/usr/bin/pip completion --bash \
    | install -Dm644 /dev/stdin "$pkgdir"/usr/share/bash-completion/completions/pip
}

package_python2-pip() {
  conflicts=('python-pyinstall')
  replaces=('python-pyinstall')

  cd "$srcdir/pip-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir"

  mv "$pkgdir/usr/bin/pip" "$pkgdir/usr/bin/pip2"
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
      ${pkgdir}/usr/lib/python2.7/site-packages/pip/__init__.py
  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/pip/__init__.py

  install -D -m644 LICENSE.txt \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  if [[ -n "$(pacman -Qq python-sphinx 2>/dev/null)" ]];then
    install -Dm644 -t "$pkgdir"/usr/share/man/man1 docs/build/man-pip2/*
  fi

  PYTHONPATH="$pkgdir"/usr/lib/python2.7/site-packages "$pkgdir"/usr/bin/pip2 completion --bash \
    | install -Dm644 /dev/stdin "$pkgdir"/usr/share/bash-completion/completions/pip2
}
