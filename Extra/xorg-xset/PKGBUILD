#-*-mode:sh-*-
_pkgname=xset
pkgname=xorg-xset
pkgver=1.2.4.r2.g4427d95
pkgrel=1
pkgdesc="User preference utility for X"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxmu')
makedepends=('xorg-util-macros')
source=(xorg/app/xset::git://anongit.freedesktop.org/git/xorg/app/xset)
sha512sums=('SKIP')
groups=('xorg')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed "s/$_pkgname.//g"
}

prepare(){
  cd "$_pkgname"
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --without-fontcache --without-xf86misc
}

build() {
  cd "$_pkgname"
  make
}

package() {
  cd "$_pkgname"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
