#-*-mode:sh-*-
pkgbase=wayland
pkgname=(wayland)
pkgver=1.16.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='https://wayland.freedesktop.org/'
license=('MIT')
depends=('glibc' 'libffi' 'expat' 'libxml2')
_makedeps=('doxygen' 'graphviz')
makedepends=('libxslt' 'xmlto' 'docbook-xsl')
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611'  # Bryce Harrington
              'C0066D7DB8E9AC6844D728715E54498E697F11D7') # Derek Foreman
source=("https://wayland.freedesktop.org/releases/$pkgbase-$pkgver.tar.xz"{,.sig})
sha1sums=('24c63a5045c653dcfa24efd10fa7c7de89aca9ef'
          'SKIP')

_confargs=('--prefix=/usr' '--disable-static')
for _m in "${_makedeps[@]}";do
  if [[ -n "$(pacman -Ss "^${_m}$" 2>/dev/null)" ]];then
    depends+=( "${_m}")
  else
    optdepends+=( "${_m}")
  fi
done

if [[ -n "$(pacman -Qq doxygen 2>/dev/null)" ]] &&
     [[ -n "$(pacman -Qq graphviz 2>/dev/null)" ]];then
  pkgname+=( 'wayland-docs')
  _confargs+=( '--enable-documentation')
else
  _confargs+=( '--disable-documentation')
fi

prepare() {
  cd $pkgbase-$pkgver
  ./configure ${_confargs[@]}    
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
}

build() {
  cd $pkgbase-$pkgver
  make
}

check() {
  cd $pkgbase-$pkgver

  make check
}

package_wayland() {
  cd $pkgbase-$pkgver

  make DESTDIR="$pkgdir" install

  if [[ -n "$(pacman -Qq doxygen 2>/dev/null)" ]] &&
       [[ -n "$(pacman -Qq graphviz 2>/dev/null)" ]];then
    mkdir -p "$srcdir/docs/share"
    mv "$pkgdir"/usr/share/{doc,man} "$srcdir/docs/share"
  fi

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_wayland-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  cd $pkgbase-$pkgver

  mv "$srcdir/docs" "$pkgdir/usr"

  install -Dm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
