#-*-mode:sh-*-
pkgname=asciidoc
pkgver=8.6.10
pkgrel=1
pkgdesc='Text document format for short documents, articles, books and UNIX man pages.'
arch=('any')
url='http://www.methods.co.nz/asciidoc/'
license=('GPL')
depends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('lilypond: music-filter'
            'imagemagick: music-filter (used in conjunction with lilypond)'
            'source-highlight: source-highlight-filter'
            'dblatex: pdf generation'
            'fop: alternative pdf generation'
            'lynx: text generation'
            'w3m: text generation (alternative to lynx)')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/asciidoc/asciidoc/archive/${pkgver}.tar.gz")
md5sums=('4e69960f4d431780e9828f53417d8d7a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i -e 's#python a2x.py#python2 a2x.py#' Makefile.in
}

build() {
  cd ${pkgname}-${pkgver}
  autoconf
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --docdir=/usr/share/doc/${pkgname}-${pkgver}
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}
  make docs DESTDIR=${pkgdir}

  install -Dm644 asciidocapi.py \
          ${pkgdir}/usr/lib/python2.7/site-packages/asciidocapi.py

  # fix FS#21579 - [asciidoc] 8.6.2-2 "asciidoc" table style doesn't work (mismatched python version) 
  #sed -i 's/python/python2/' \
    #${pkgdir}/etc/asciidoc/asciidoc.conf
}
