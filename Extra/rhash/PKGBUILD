#-*-mode:sh-*-
pkgname=rhash
pkgver=1.3.6
pkgrel=1
pkgdesc='Utility for verifying hash sums of files'
url='http://rhash.anz.ru/?l=en'
license=('custom' 'MIT')
arch=('x86_64')
depends=('glibc')
backup=('etc/rhashrc')
source=(https://downloads.sourceforge.net/rhash/rhash-$pkgver-src.tar.gz{,.asc})
validpgpkeys=('2875F6B1C2D27A4F0C8AF60B2A714497E37363AE')
sha256sums=('964df972b60569b5cb35ec989ced195ab8ea514fc46a74eab98e86569ffbcf92'
            'SKIP')

prepare() {
  cd RHash-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --extra-cflags="$CFLAGS" --extra-ldflags="$LDFLAGS"
}

build() {
  cd RHash-$pkgver
  make
}

check() {
  cd RHash-$pkgver
  make test test-lib
}

package() {
  cd RHash-$pkgver

  # program
  make DESTDIR="$pkgdir" install

  # library, since the makefiles are still broken
  rm "$pkgdir"/usr/bin/rhash
  make -C librhash DESTDIR="$pkgdir" install-headers install-lib-shared install-so-link
  install -m644 rhash "$pkgdir"/usr/bin/rhash

  # license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/rhash/LICENSE
}

