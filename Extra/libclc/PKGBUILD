#-*-mode:sh-*-
pkgname=libclc
pkgver=0.2.0+539+dabae5a
pkgrel=1
pkgdesc="Library requirements of the OpenCL C programming language"
arch=('any')
url="http://libclc.llvm.org/"
license=('MIT')
makedepends=('clang' 'llvm' 'python2' 'git')
options=('staticlibs')
source=('git+http://llvm.org/git/libclc.git#commit=dabae5a')
md5sums=('SKIP')

pkgver() {
  cd libclc
  echo 0.2.0+$(git rev-list --count HEAD)+$(git describe --always)
}

prepare(){
  cd libclc
  sed -i 's/"python < $in >/sys.executable + " < $in >/g' configure.py
  python2 ./configure.py --prefix=/usr

}

build() {
  cd libclc
  make
}

package() {
  cd libclc
  make install DESTDIR="$pkgdir"
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

