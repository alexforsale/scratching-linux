#-*-mode:sh-*-
pkgname=vala
pkgver=0.43.2+17+g2cea36afe
_pkgver=0.43.2
pkgrel=1
pkgdesc='Compiler for the GObject type system'
url='https://wiki.gnome.org/Projects/Vala'
arch=(x86_64)
license=(LGPL)
_deps=(gtk-doc)
depends=(glib2 graphviz ttf-font pkg-config gcc)
makedepends=(vala libxslt git help2man)
checkdepends=(dbus libx11 gobject-introspection)
provides=(valadoc libvala-${pkgver%.*}.so libvaladoc-${pkgver%.*}.so)
conflicts=(valadoc)
replaces=(valadoc)
source=("git+https://gitlab.gnome.org/GNOME/vala.git")
sha256sums=('SKIP')

for _d in "${_deps[@]}";do
    if [[ -n "$(pacman -Ss "^${_d}$" 2>/dev/null)" ]];then
        depends+=( "${_d}")
    else
        optdepends+=( "${_d}")
    fi
done

if [[ -z "$(pacman -Qq "^vala$" 2>/dev/null)" ]];then
    source+=("https://download.gnome.org/sources/$pkgname/${_pkgver%.*}/$pkgname-$_pkgver.tar.xz")
    sha256sums+=('713cb9464649c851b0050b6f9b6b91fd6157f23e7ab5943ceb1883538f86d722')
fi

_prepare_tarball(){
    mkdir -pv ${srcdir}/{vala-build,valac}
    cd ${srcdir}/vala-build
    ${srcdir}/${pkgname}-${_pkgver}/configure --prefix=${srcdir}/valac
    msg2 "compiling vala from tarball for bootstrapping"
    make
    make install
}

pkgver() {
    cd ${srcdir}/${pkgname}
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    if [[ -z "$(pacman -Ss "^vala$" 2>/dev/null)" ]];then
        _prepare_tarball
        export PATH=${PATH}:${srcdir}/valac/bin
    fi

    cd ${srcdir}/$pkgname
    NOCONFIGURE=1 ./autogen.sh
    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
}

build() {
    cd ${srcdir}/${pkgname}
    make
}

check() {
    cd ${srcdir}/${pkgname}
    make check
}

package() {
    cd ${srcdir}/${pkgname}
    make DESTDIR="$pkgdir" install
}
