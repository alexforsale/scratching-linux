#-*-mode:sh-*-
pkgname=libuv
pkgver=1.24.0
pkgrel=1
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('x86_64')
url="https://github.com/libuv/libuv"
license=('custom')
_deps=('libnsl')
depends=('glibc')
_makedeps=('python-sphinx' 'python-six' 'python-pyparsing')
makedepends=()
source=("https://github.com/libuv/libuv/archive/v$pkgver/$pkgname-$pkgver.tar.gz")

for _d in "${_deps[@]}";do
  if [[ -n "$(pacman -Ss "^${_d}$" 2>/dev/null)" ]];then
    depends+=( "${_d}")
  else
    optdepends+=( "${_d}")
  fi
done
for _m in "${_makedeps[@]}";do
  if [[ -n "$(pacman -Ss "^${_m}$" 2>/dev/null)" ]];then
    makedepends+=( "${_m}")
  else
    optdepends+=( "${_m}")
  fi
done

prepare(){
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --disable-static
}

build() {
  cd $pkgname-$pkgver
  make
  if [[ -z "$(pacman -Qq python-sphinx 2>&1 >/dev/null)" ]];then
    make man -C docs
  fi
}

check() {
  cd $pkgname-$pkgver
  make check || true
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE \
	  "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 AUTHORS \
	  "$pkgdir"/usr/share/doc/$pkgname/AUTHORS
  install -Dm644 README.md \
	  "$pkgdir"/usr/share/doc/$pkgname/README.md
  install -Dm644 ChangeLog \
	  "$pkgdir"/usr/share/doc/$pkgname/ChangeLog
  if [[ -z "$(pacman -Qq python-sphinx 2>&1 >/dev/null)" ]];then
    install -Dm644 docs/build/man/libuv.1 \
	    "$pkgdir"/usr/share/man/man1/libuv.1
  fi
}
sha512sums=('b75dac19e99497b059b9b8ce646266c4fa10f1259f5fd74554f7f614f7b70f4b62b9c756316d9a3eed6cf6b9d61f5acde9f6eedb88d30431f6a5c87169487ec6')
