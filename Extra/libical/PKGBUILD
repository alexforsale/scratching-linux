#-*-mode:sh-*-
pkgname=libical
pkgver=3.0.4
pkgrel=1
pkgdesc="An open source reference implementation of the icalendar data type and serialization format"
arch=('x86_64')
url='https://github.com/libical/libical'
license=('LGPL' 'MPL')
depends=('glibc' 'glib2' 'icu' 'berkeley-db')
makedepends=('cmake' 'ninja')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('20f39343701ccd3ad896a9f9e982fdf85c1d3a35572e9d962216b69a64aef2ae')

prepare() {
  mkdir build && cd $_
  cmake ../"${pkgname}-${pkgver}" \
        -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DSHARED_ONLY=true \
        -DICAL_BUILD_DOCS=false \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib
}

build() {
  cd build
  ninja
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}
