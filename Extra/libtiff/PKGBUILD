#-*-mode:sh-*-
pkgname=libtiff
pkgver=4.0.10
pkgrel=1
pkgdesc='Library for manipulation of TIFF images'
url='http://www.simplesystems.org/libtiff/'
arch=('x86_64')
license=('custom')
depends=('libjpeg' 'zlib' 'xz' 'zstd')
makedepends=('freeglut' 'glu' 'mesa' 'jbigkit')
optdepends=('freeglut: for using tiffgt')
source=(https://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz{,.sig})
sha256sums=('2c52d11ccaf767457db0c46795d9c7d1a8d8f76f68b0b800a3dfe45786b996e4'
            'SKIP')
validpgpkeys=('EBDFDB21B020EE8FD151A88DE301047DE1198975') # Bob Friesenhahn <bfriesen@simple.dallas.tx.us>

prepare(){
  cd tiff-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --with-docdir=/usr/share/doc/${pkgname}
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
}

build() {
  cd tiff-${pkgver}
  make
}

check() {
  cd tiff-${pkgver}
  make check
}

package() {
  cd tiff-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
sha256sums=('2c52d11ccaf767457db0c46795d9c7d1a8d8f76f68b0b800a3dfe45786b996e4'
            'SKIP')
