#-*-mode:sh-*-
_pkgname=drm
pkgname=libdrm
pkgver=lib2.4.96.r20.gcfab2fc3
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('valgrind' 'libxslt' 'docbook-xsl' 'meson')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
source=(xorg/mesa/drm::git://anongit.freedesktop.org/git/mesa/drm
        no-drmdevice-test.diff
        COPYING)
groups=('xorg')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed "s/${_pkgname}.//g"
}

prepare() {
  cd $_pkgname

  # Fails in a container; autotools skipped this one already
  patch -Np1 -i ../no-drmdevice-test.diff

  cd ..
  meson-helper $_pkgname build \
    -Dudev=false \
    -Dvalgrind=false
}

build() {
  meson-helper $_pkgname build \
    -Dudev=false \
    -Dvalgrind=false
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

sha512sums=('SKIP'
            'f1dd5d8c2270c092ccb8e4f92a0da9ab27706dfa22dcedd3fb2414b968ced9333c8bf62baf0219b822e43dce0d804d1dd5cc27d09b0afe8c01967c1784d4a4bb'
            'b0ca349b882a4326b19f81f22804fabdb6fb7aef31cdc7b16b0a7ae191bfbb50c7daddb2fc4e6c33f1136af06d060a273de36f6f3412ea326f16fa4309fda660')
