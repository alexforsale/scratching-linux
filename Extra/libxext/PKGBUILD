#-*-mode:sh-*-
_pkgname=libXext
pkgname=libxext
pkgver=1.3.3.r8.g7ecd11d
pkgrel=1
pkgdesc="X11 miscellaneous extensions library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'xextproto')
makedepends=('xorg-util-macros')
source=(xorg/lib/libXext::git://anongit.freedesktop.org/git/xorg/lib/libXext)
sha512sums=('SKIP')
groups=('xorg')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed "s/${_pkgname/-/.}.//g"
}

prepare(){
  cd "$_pkgname"
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
}

build() {
  cd "$_pkgname"
  make
}

package() {
  cd "$_pkgname"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
