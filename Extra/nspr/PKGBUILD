#-*-mode:sh-*-
pkgname=nspr
pkgver=4.20
pkgrel=1
pkgdesc="Netscape Portable Runtime"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSPR"
arch=(x86_64)
license=(MPL GPL)
depends=(glibc sh)
makedepends=(zip)
source=(https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/nspr-${pkgver}.tar.gz)
sha1sums=('ef1e2ca3205fd1658a69ada2e0436266ca3065b5')
sha256sums=('2c8964913da89ffbaf464d49ce44d79e8804e1794ef9a8c52a7bff7224d1556e')

prepare() {
    cd nspr-$pkgver/nspr
    sed -ri 's#^(RELEASE_BINS =).*#\1#' pr/src/misc/Makefile.in
    sed -i 's#$(LIBRARY) ##' config/rules.mk
    
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --includedir=/usr/include/nspr \
    --enable-optimize \
    --disable-debug \
    $([[ $CARCH == x86_64 ]] && echo --enable-64bit)
}

build() {
  cd nspr-$pkgver/nspr
  make
}

package() {
  cd nspr-$pkgver/nspr
  make DESTDIR="$pkgdir" install
  ln -s nspr.pc "$pkgdir/usr/lib/pkgconfig/mozilla-nspr.pc"
  rm -rf "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties} \
     "$pkgdir"/usr/include/nspr/md
}
