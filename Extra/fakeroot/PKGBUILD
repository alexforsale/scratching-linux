#-*-mode:sh -*-
pkgname=fakeroot
pkgver=1.23
pkgrel=1
pkgdesc='Tool for simulating superuser privileges'
arch=(x86_64)
license=(GPL)
url='http://packages.debian.org/fakeroot'
install=fakeroot.install
_deps=('util-linux')
depends=('glibc'
         'filesystem'
         'sed'
         'sh'
        )
_checkdeps=('sharutils')
checkdepends=()
source=(http://ftp.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.xz
        silence-dlerror.patch)
groups=('lfs-base')

for _c in "${_checkdeps[@]}";do
  if [[ -n "$(pacman -Ss "^${_c}$" 2>/dev/null)" ]];then
    depends+=( "${_c}")
  else
    optdepends+=( "${_c}")
  fi
done
for _d in "${_deps[@]}";do
  if [[ -n "$(pacman -Ss "^${_d}$" 2>/dev/null)" ]];then
    depends+=( "${_d}")
  else
    optdepends+=( "${_d}")
  fi
done

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/silence-dlerror.patch

  ./bootstrap
  ./configure --prefix=/usr \
              --libdir=/usr/lib/libfakeroot \
              --disable-static \
              --with-ipc=sysv
}

build(){
  cd $pkgname-$pkgver
  make
}

check() {
  cd $pkgname-$pkgver
  make check || true
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/etc/ld.so.conf.d/"
  echo '/usr/lib/libfakeroot' > "$pkgdir/etc/ld.so.conf.d/fakeroot.conf"

  # install README for sysv/tcp usage
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

md5sums=('b82c5e99b6365a838e73d05718083f6a'
         'ad229f38737fa0a1cc0bea4f0db4a2be')
