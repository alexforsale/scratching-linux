#-*-mode:sh-*-
pkgname=libglvnd
pkgver=1.1.0
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
_deps=('libxext' 'mesa' 'opengl-driver') # libglvnd needs mesa for indirect rendering
makedepends=('libxext' 'libx11' 'glproto' 'python')
provides=('libgl' 'libegl' 'libgles')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NVIDIA/libglvnd/archive/v$pkgver.tar.gz"
        LICENSE)

for _d in "${_deps[@]}";do
  if [[ -n "$(pacman -Ss "^${_d}$" 2>/dev/null)" ]];then
    depends+=( "${_d}")
  else
    optdepends+=( "${_d}")
  fi
done

prepare(){
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
sha512sums=('40c81f334f44927af4ebdd291f4447d4c01cbaa9f39ebcd519c86e360357b7427b574514a70b62c9538dc90fa312ed632f49cc5e818a800e23b250ec92f4ae29'
            '085be7058af8f4f9a9bb540fec3e74b77cb92cca8fc3e8f4a8dd4cd404acc2e700f8b141f17818c056d76d1085ec63a33a52f13c49b902689ae000ff1a0944bf')
