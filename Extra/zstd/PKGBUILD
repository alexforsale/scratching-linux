#-*-mode:sh-*-
pkgname=zstd
pkgver=1.3.7
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm'
arch=(x86_64)
url='http://www.zstd.net/'
license=(BSD GPL2)
depends=(zlib xz lz4)
makedepends=(gtest)
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.gz)

build() {
  cd $pkgname-$pkgver
  make
  make zstdmt
  make -C contrib/pzstd
}

check() {
  cd $pkgname-$pkgver
  make check    
  make -C contrib/pzstd test
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -Dm755 zstdmt "$pkgdir/usr/bin/zstdmt"
  install -Dm755 contrib/pzstd/pzstd "$pkgdir/usr/bin/pzstd"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
sha256sums=('3277f236df0ca6edae01ae84e865470000c5a3484588fd5bc3d869877fd3573d')
