#-*-mode:sh-*-
pkgname=libpciaccess
pkgver=0.14.r3.gfbed5d2
pkgrel=1
pkgdesc="X11 PCI access library"
arch=(x86_64)
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-util-macros')
source=(xorg/lib/libpciaccess::git://anongit.freedesktop.org/git/xorg/lib/libpciaccess)
groups=('xorg')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed "s/${pkgname}.//g"
}

prepare(){
  cd ${pkgname}
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
}

build() {
  cd ${pkgname}
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
md5sums=('SKIP')
