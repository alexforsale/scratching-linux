#-*-mode:sh-*-
pkgname=screen
pkgver=4.6.2
pkgrel=1
pkgdesc='Full-screen window manager that multiplexes a physical terminal'
url='https://www.gnu.org/software/screen/'
arch=('x86_64')
license=('GPL')
depends=('ncurses' 'linux-pam')
validpgpkeys=('2EE59A5D0C50167B5535BBF1B708A383C53EF3A4'
              '71AA09D9E8870FDB0AA7B61E21F968DEF747ABD7')
source=("https://ftp.gnu.org/gnu/screen/screen-${pkgver}.tar.gz"{,.sig}
        'pam.d')
backup=('etc/screenrc' 'etc/pam.d/screen')
options=('!makeflags')

_ptygroup=5 #the UID of our PTY/TTY group

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		--enable-colors256 \
		--enable-pam \
		--enable-rxvt_osc \
		--enable-telnet \
		--with-pty-group=$_ptygroup \
		--with-socket-dir=/run/screens \
		--with-sys-screenrc=/etc/screenrc \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../pam.d "${pkgdir}"/etc/pam.d/screen

	install -Dm644 etc/etcscreenrc "${pkgdir}"/etc/screenrc
	install -Dm644 etc/screenrc "${pkgdir}"/etc/skel/.screenrc
}

md5sums=('a0f529d3333b128dfaa324d978ba73a8'
         'SKIP'
         '74f7919d7b65436cc2ea5341fa8ca374')
